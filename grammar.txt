<程序> -> <main关键字> ( ) { <声明序列> <语句序列> }
<声明序列> -> <声明语句> <剩余序列>
<剩余序列> -> <声明语句> <剩余序列> | @
<声明语句> -> int <标识符表> ;
<标识符表> -> identifier <其他标识符>
<其他标识符> -> , <标识符表> | @
<语句序列> -> <语句> <其他语句序列>
<其他语句序列> -> <语句> <其他语句序列> | @
<语句> -> <if语句> | <while语句> | <for语句> | <复合语句> | <赋值语句>
<if语句> -> <if关键字> ( <表达式> ) <复合语句> <else语句>
<else语句> -> <else关键字> <复合语句> | @
<while语句> -> <while关键字> ( <表达式> ) <复合语句>
<for语句> -> <for关键字> ( <表达式> ; <表达式> ; <表达式> ) <复合语句>
<复合语句> -> { <语句序列> }
<赋值语句> -> identifier = <表达式> ;
<表达式> -> <算数表达式> <其他表达式>
<其他表达式> -> <关系运算符> <算数表达式> | @
<关系运算符> -> > | < | >= | <= | == | !=
<算数表达式> -> <项> <与项运算>
<与项运算> -> + <项> <与项运算> | - <项> <与项运算> | @
<项> -> <因子> <因子其他部分>
<因子其他部分> -> <与因子运算> <因子其他部分> | @
<与因子运算> -> * <因子> | / <因子>
<因子> -> identifier | number | ( <算数表达式> )
<main关键字> -> main
<if关键字> -> if
<else关键字> -> else
<for关键字> -> for
<while关键字> -> while
<int关键字> -> int